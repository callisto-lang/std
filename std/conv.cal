include "array.cal"
include "ops.cal"

## ## parse_dec
## Parameters: Array* string
##
## Converts decimal integer representation in string into an integer
func error parse_dec ptr Array arr -> cell res begin
	let cell pos 0 -> pos
	0 while pos arr.length < do
		pos arr a@
		if dup is_dec_digit then
			'0' - swap 10 * +
		else
			unsafe cell _ -> begin
				drop drop c"parse_dec: Invalid digit" throw
			end
		end

		pos 1 + -> pos
	end
end

## ## parse_hex
## Parameters: Array* string
##
## Converts hexadecimal integer representation in string into an integer
func error parse_hex ptr Array arr -> cell res begin
	let cell pos 0 -> pos
	0 while pos arr.length < do
		pos arr a@
		if dup is_dec_digit then
			'0' - swap 16 * +
		elseif dup 'a' 'f' in_range then
			'a' - 10 + swap 16 * +
		elseif dup 'A' 'F' in_range then
			'A' - 10 + swap 16 * +
		else unsafe cell _ -> begin
			drop drop c"parse_hex: Invalid digit" throw
		end end

		pos 1 + -> pos
	end
end

## ## parse_bin
## Parameters: Array* string
##
## Converts binary integer representation in string into an integer
func error parse_bin ptr Array arr -> cell res begin
	let cell pos 0 -> pos
	0 while pos arr.length < do
		pos arr a@
		if dup is_bin_digit then
			'0' - swap 2 * +
		else unsafe cell _ -> begin
			drop drop c"parse_bin: Invalid digit" throw
		end end

		pos 1 + -> pos
	end
end
